+++
title = "â„ï¸ NixOSã¨Raspberry Piã§è‡ªå®…server | Part 2 deploy-rsã§deploy"
slug = "homeserver-with-nixos-and-raspberrypi-deploy-with-deploy-rs"
description = "deploy-rsã‚’ä½¿ã£ã¦Raspberry Piã«deploy"
date = "2023-10-09T01:00:00Z"
draft = false
[taxonomies]
tags = ["nix"]
[extra]
image = "images/emoji/snowflake.png"
+++

[Part 1 NixOSã‚’install](https://blog.ymgyt.io/entry/homeserver-with-nixos-and-raspberrypi-install-nixos/)  
Part 2 deply-rsã§NixOS Configurationã‚’é©ç”¨(ğŸ‘ˆ ã“ã®è¨˜äº‹)  
[Part 3 ragenixã§secretç®¡ç†](https://blog.ymgyt.io/entry/homeserver-with-nixos-and-raspberrypi-secret-management-with-ragenix/)  
[Part 4 opentelemetry-collectorã¨openobserveã§metricsã‚’å–å¾—](https://blog.ymgyt.io/entry/homeserver-with-nixos-and-raspberrypi-export-metrics-with-opentelemetry-collector/)  

æœ¬è¨˜äº‹ã¯NixOSã¨Raspberry Piã§è‡ªå®…serverã‚’ã¯ã˜ã‚ã‚‹è¨˜äº‹ã®Part 2ã§ã™ã€‚   
Part 1ã§Raspberry Pi(ä»¥ä¸‹raspi)ã«NixOSã‚’installã—ã¦sshã§ãã‚‹ã¨ã“ã‚ã¾ã§ã‚’è¡Œã„ã¾ã—ãŸã€‚  
æœ¬è¨˜äº‹ã§ã¯raspiã®è¨­å®šã‚’falkeã§ç®¡ç†ã—ã¦æ‰‹å…ƒã®host machineã‹ã‚‰deploy(é©ç”¨)ã§ãã‚‹ã‚ˆã†ã«ã—ã¦ã„ãã¾ã™ã€‚  
å®Ÿéš›ã®sourceã¯[ã“ã¡ã‚‰](https://github.com/ymgyt/mynix/tree/main/homeserver)ã§ç®¡ç†ã—ã¦ã„ã¾ã™ã€‚ 

æ¦‚è¦ã¨ã—ã¦ã¯[deploy-rs]ã‚’åˆ©ç”¨ã™ã‚‹ã“ã¨ã§ansibleã§æ§‹æˆç®¡ç†ã™ã‚‹ã®ã¨è¿‘ã„ã“ã¨ãŒã§ãã¾ã™ã€‚  
é•ã†ã®ã¯provisioningã®è¨­å®šã‚’`nixosConfiguration`ã§è¡Œãˆã‚‹ç‚¹ã§ã™ã€‚ã“ã‚Œã«ã‚ˆã‚Šhostã®NixOSã‚„Macã¨åŒã˜è¨­å®šã®ä»•çµ„ã¿ã§raspiã‚‚ç®¡ç†ã§ãã¾ã™ã€‚  
NixOSã‚„Macã‚’nixã§ç®¡ç†ã—ã¦ã„ãæ–¹æ³•ã«ã¤ã„ã¦ã¯ä»¥å‰[Nixã§linuxã¨macã®ç’°å¢ƒã‚’ç®¡ç†ã—ã¦ã¿ã‚‹](https://blog.ymgyt.io/entry/declarative-environment-management-with-nix/)ã§æ›¸ã„ã¦ã¿ã¾ã—ãŸã€‚


raspiã‚’ç®¡ç†ã™ã‚‹repositoryã®`flake.nix`ã®å…¨ä½“ã¨ã—ã¦ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚ 
ã“ã®è¨­å®šã‚’ç†è§£ã™ã‚‹ã®ãŒç›®æ¨™ã§ã™ã€‚

```nix
{
  description = "Deployment for my home server cluster";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    deploy-rs = {
      url = "github:serokell/deploy-rs";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.utils.follows = "flake-utils";
    };

    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs =
    { self, nixpkgs, deploy-rs, flake-utils}:
    let
      spec = {
        user = "ymgyt";
        defaultGateway = "192.168.10.1";
        nameservers = [ "8.8.8.8" ];
      };
    in {
      nixosConfigurations = {
        rpi4-01 = nixpkgs.lib.nixosSystem {
          system = "aarch64-linux";
          specialArgs = spec;
          modules = [ ./hosts/rpi4-01.nix ];
        };
        # ... per host...
        rpi4-04 = nixpkgs.lib.nixosSystem {
          system = "aarch64-linux";
          specialArgs = spec;
          modules = [ ./hosts/rpi4-04.nix ];
        };
      };

      deploy = {
        # Deployment options applied to all nodes
        sshUser = spec.user;
        # User to which profile will be deployed.
        user = "root";
        sshOpts = [ "-p" "22" "-F" "./ssh.config" ];

        fastConnection = false;
        autoRollback = true;
        magicRollback = true;

        # Or setup cross compilation
        remoteBuild = true;

        nodes = {
          rpi4-01 = {
            hostname = "rpi4-01";
            profiles.system = {
              path = deploy-rs.lib.aarch64-linux.activate.nixos
                self.nixosConfigurations.rpi4-01;
            };
          };
          # ... per host
          rpi4-04 = {
            hostname = "rpi4-04";
            profiles.system = {
              path = deploy-rs.lib.aarch64-linux.activate.nixos
                self.nixosConfigurations.rpi4-04;
            };
          };
        };
      };

      checks = builtins.mapAttrs
        (system: deployLib: deployLib.deployChecks self.deploy) deploy-rs.lib;
    } // flake-utils.lib.eachDefaultSystem (system:
      let pkgs = import nixpkgs { inherit system; };
      in {
        devShells.default =
          pkgs.mkShell { buildInputs = [ pkgs.deploy-rs pkgs.nixfmt ]; };
      });
}
```

## Raspberry Piã®è¨­å®š

ã¾ãšã¯raspiã®è¨­å®šã‚’è¡Œã£ã¦ã„ãã¾ã™ã€‚  

```nix
{
  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable"; # ...
  };

  outputs =
    { self, nixpkgs, ...}:
    let
      spec = {
        user = "ymgyt";
        defaultGateway = "192.168.10.1";
        nameservers = [ "8.8.8.8" ];
      };
    in {
      nixosConfigurations = {
        rpi4-01 = nixpkgs.lib.nixosSystem {
          system = "aarch64-linux";
          specialArgs = spec;
          modules = [ ./hosts/rpi4-01.nix ];
        };
      # ...
    };
}
```

raspiã®host `rpi4-01`ã®è¨­å®šã¯ä¸Šè¨˜ã®ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚  
hoståã¯[Part 1]ã§è¨­å®šã—ã¦ã‚ã‚‹å‰æã§ã™ã€‚  
è¨­å®šè‡ªä½“ã¯é€šå¸¸ã®`nixosConfigurations`ã§è¡Œã„ã¾ã™ã€‚  
`specialArgc = spce`ã‚’æŒ‡å®šã™ã‚‹ã“ã¨ã§moduleã‹ã‚‰å„ç¨®è¨­å®šå‚ç…§ã§ãã‚‹ã‚ˆã†ã«ã—ã¦ã‚ã‚Šã¾ã™ã€‚  
`system`ã¯raspiãªã®ã§`aarch64-linux`ã‚’æŒ‡å®šã—ã¾ã—ãŸã€‚   

èª¿ã¹æ–¹ã¨ã—ã¦ã¯`nix repl`ã‚’åˆ©ç”¨ã—ã¾ã—ãŸã€‚

```sh
$ nix repl
Welcome to Nix 2.17.0. Type :? for help.

nix-repl> builtins.currentSystem
"aarch64-linux"
```

moduleã¨ã—ã¦å‚ç…§ã—ã¦ã„ã‚‹`./hosts/rpi4-01.nix`ã¯ä»¥ä¸‹ã®ã‚ˆã†ã«ãªã£ã¦ã„ã¾ã™ã€‚

```nix
{ defaultGateway, nameservers, ... }: {
  imports = [ ../modules/rpi4.nix ];

  networking = {
    inherit defaultGateway nameservers;
    hostName = "rpi4-01";
    interfaces.end0.ipv4.addresses = [{
      address = "192.168.10.150";
      prefixLength = 24;
    }];
    wireless.enable = false;
  };
}
```

`hosts/`é…ä¸‹ã¯hostæ¯ã®è¨­å®šã‚’å®šç¾©ã—ã¦ã„ã¾ã™ã€‚  
åŸºæœ¬çš„ã«ã¯[Part 1]ã§ãŠã“ãªã£ãŸè¨­å®šã‹ã‚‰hostå›ºæœ‰ã®è¨­å®šã‚’ã“ã“ã§å®šç¾©ã—ã¦ã„ã¾ã™ã€‚  
raspiãã‚Œãã‚Œã§å…±é€šã™ã‚‹è¨­å®šã¯`modules/rpi4.nix`ã«å®šç¾©ã—ã¾ã—ãŸã€‚  

```nix
# Common settings for Raspberry Pi4 Model B
{ pkgs, user, ... }: {

  boot = {
    # ...
  };

  fileSystems = {
    # ...
  };

  environment.systemPackages = with pkgs; [ helix git bottom bat ];

  # ...
  users = {
    # ...
  };
  security.sudo.wheelNeedsPassword = false;

  hardware.enableRedistributableFirmware = true;
  system.stateVersion = "23.11";
}
```

è¨­å®šè‡ªä½“ã¯[Part 1]ã¨åŒã˜ã§ã™ã€‚ 

## deploy-rs

ç¶šã„ã¦å®šç¾©ã—ãŸraspiã®è¨­å®šã‚’deployã™ã‚‹æ–¹æ³•ã‚’è¦‹ã¦ã„ãã¾ã™ã€‚ã€€

```nix
{
  inputs = {
    # ...
    deploy-rs = {
      url = "github:serokell/deploy-rs";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.utils.follows = "flake-utils";
    };
  };

  outputs =
    { self, nixpkgs, deploy-rs, ...}:
    let
     # ...
    in {
      nixosConfigurations = {
        rpi4-01 = nixpkgs.lib.nixosSystem {
          system = "aarch64-linux";
          specialArgs = spec;
          modules = [ ./hosts/rpi4-01.nix ];
        };
        # ...
      };

      deploy = {
        # Deployment options applied to all nodes
        sshUser = spec.user;
        # User to which profile will be deployed.
        user = "root";
        sshOpts = [ "-p" "22" "-F" "./etc/ssh.config" ];

        fastConnection = false;
        autoRollback = true;
        magicRollback = true;

        # Or setup cross compilation
        remoteBuild = true;

        nodes = {
          rpi4-01 = {
            hostname = "rpi4-01";
            profiles.system = {
              path = deploy-rs.lib.aarch64-linux.activate.nixos
                self.nixosConfigurations.rpi4-01;
            };
          };
          # for each node...
          rpi4-04 = {
            hostname = "rpi4-04";
            profiles.system = {
              path = deploy-rs.lib.aarch64-linux.activate.nixos
                self.nixosConfigurations.rpi4-04;
            };
          };
        };
      };
      # ...
}
```
[deploy-rs]ã¯flakeã®outputsã«`deploy`ã‚’æœŸå¾…ã—ã¾ã™ã€‚  
`deploy.{sshUser,user}`ç­‰ã¯å„raspiã¸ã®deployå…±é€šã®è¨­å®šã§ã™ã€‚  
`deploy.autoRollback`ã¯deployãŒå¤±æ•—ã—ãŸå ´åˆã«å‰å›ã®profileã‚’æœ‰åŠ¹ã«ã—ã¦ãã‚Œã‚‹optionã§ã™ã€‚  
ã“ã“ã§ã„ã†profileã¯[nixã®profile](https://nixos.org/manual/nix/stable/package-management/profiles)ã®ã“ã¨ã ã¨ç†è§£ã—ã¦ã„ã¾ã™ã€‚ 
`deploy.magicRollback`ã«ã¤ã„ã¦ã¯[README](https://github.com/serokell/deploy-rs#magic-rollback)ã«ã‚ˆã‚‹ã¨ãªã‚“ã‚‰ã‹ã®ç†ç”±ã§å¤‰æ›´å¾Œã«deployå¯¾è±¡nodeã¸ã®ç–é€šãŒã§ããªããªã£ãŸå ´åˆã«è‡ªå‹•ã§rollbackã—ã¦ãã‚Œã‚‹è¨­å®šã§ã™ã€‚  

`deploy.remoteBuild`ã¯trueã‚’è¨­å®šã—ã¦ã„ã¾ã™ã€‚ 
è‡ªåˆ†ã®host machineã«`aarch64-linux`ã®ã‚‚ã®ãŒãªãã€nixã®cross compileã‚’ã¾ã è¨­å®šã§ãã¦ã„ãªã„ãŸã‚ã€deployå…ˆã®raspiä¸Šã§buildã™ã‚‹ã‚ˆã†ã«ã—ã¦ã„ã¾ã™ã€‚  

`deploy.nodes.${node}`ã«deployå¯¾è±¡ã®nodeã‚’è¨­å®šã—ã¾ã™ã€‚  
nodeã”ã¨ã«profileã‚’æŒ‡å®šã™ã‚‹ã“ã¨ãŒã§ãã“ã‚ŒãŒdeployã®å˜ä½ã¨ãªã‚Šã¾ã™ã€‚profile = roleã¨ã„ã†æ„Ÿã˜ã§ã™ã€‚  
`deploy.nodes.${node}.profiles.${profile}.path`ã«ã•ãã»ã©å®šç¾©ã—ãŸnixosConfigurationã‚’æŒ‡å®šã—ã¾ã™ã€‚  

ã“ã‚Œã§deployã®è¨­å®šã¯å®Œäº†ã§ã™ã€‚ 


## å®Ÿéš›ã«deploy

ã•ã£ããå®šç¾©ã—ãŸè¨­å®šã‚’deployã—ã¦ã¿ã¾ã™ã€‚  
`flake.nix`ã®`devShell`ã«ä»¥ä¸‹ã®ã‚ˆã†ã«deploy-rsã‚’è¿½åŠ ã—ã¾ã™ã€‚  

```nix
{
  inputs = {
    # ...
    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs =
    { self, nixpkgs, deploy-rs, flake-utils}:
    let
      # ...
    in {
      nixosConfigurations = {
        # ...
      };

      deploy = {
       # ...
      };
    } // flake-utils.lib.eachDefaultSystem (system:
      let pkgs = import nixpkgs { inherit system; };
      in {
        devShells.default =
          pkgs.mkShell { buildInputs = [ pkgs.deploy-rs pkgs.nixfmt ]; };
      });
}
```

ã“ã‚Œã§`nix develop -c nu`ã‚‚ã—ãã¯`direnv`ã§`deploy`ãŒæœ‰åŠ¹ã«ãªã‚Šã¾ã™ã€‚

ã¾ãšã¯1å°ã«deployã—ã¦ã¿ã¾ã™ã€‚  

```sh
> deploy --interactive --skip-checks .#rpi4-01
ğŸš€ â„¹ [deploy] [INFO] Evaluating flake in .
ğŸš€ â„¹ [deploy] [INFO] The following profiles are going to be deployed:
[rpi4-01.system]
user = "root"
ssh_user = "ymgyt"
path = "/nix/store/x901jwam6nzwgfxyzy7wa5hahm33fb93-activatable-nixos-system-rpi4-01-23.11.20230920.fe97767"
hostname = "rpi4-01"
ssh_opts = ["-p", "22", "-F", "./etc/ssh.config"]

ğŸš€ â„¹ [deploy] [INFO] Are you sure you want to deploy these profiles?
> yes
ğŸš€ â„¹ [deploy] [INFO] Building profile `system` for node `rpi4-01` on remote host
ğŸš€ â„¹ [deploy] [INFO] Activating profile `system` for node `rpi4-01`
ğŸš€ â„¹ [deploy] [INFO] Creating activation waiter
ğŸ‘€ â„¹ [wait] [INFO] Waiting for confirmation event...
â­ â„¹ [activate] [INFO] Activating profile
activating the configuration...
setting up /etc...
reloading user units for ymgyt...
setting up tmpfiles
reloading the following units: dbus.service
â­ â„¹ [activate] [INFO] Activation succeeded!
â­ â„¹ [activate] [INFO] Magic rollback is enabled, setting up confirmation hook...
â­ â„¹ [activate] [INFO] Waiting for confirmation event...
ğŸ‘€ â„¹ [wait] [INFO] Found canary file, done waiting!
ğŸš€ â„¹ [deploy] [INFO] Success activating, attempting to confirm activation
ğŸš€ â„¹ [deploy] [INFO] Deployment confirmed.
```

ç„¡äº‹deployã§ãã¾ã—ãŸã€‚  
`--skip-checks`ã¯[hostã¨remoteã®systemãŒé•ã†ã¨ç¾çŠ¶ã ã¨ã†ã¾ãcheckãŒå‹•ã‹ãªã„ã‚ˆã†ãªã®ã§](https://github.com/serokell/deploy-rs/issues/216)ã¤ã‘ã¦ã„ã¾ã™ã€‚

ã“ã‚Œã§raspiã®nixosConfigurationã‚’åæ˜ ã•ã›ã‚‰ã‚Œã‚‹ã‚ˆã†ã«ãªã£ãŸã®ã§ã•ã£ããè¨­å®šã‚’å¤‰æ›´ã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚  
ç¾çŠ¶ã§ã¯time zoneã‚’è¨­å®šã—ã¦ã„ãªã„ã®ã§ã“ã‚Œã‚’è¨­å®šã—ã¦ã¿ã¾ã™ã€‚  

`./modules/rpi4.nix`ã«`time.timeZone = "Asia/Tokyo";`ã‚’è¿½åŠ ã—ã¾ã™ã€‚ä»Šåº¦ã¯å…¨å°ã«åæ˜ ã•ã›ãŸã„ã®ã§

```sh
deploy --skip-checks --interactive .
```

ã‚’å®Ÿè¡Œã—ã¦ã¿ã¾ã™ã€‚
æˆåŠŸã—ãŸã‚‰[Part 1]ã§ä½œæˆã—ãŸzellij layoutã‚’ä½¿ã£ã¦sshã—ãŸã®ã¡`timedatectl`ã‚’å®Ÿè¡Œã—ã¦ã¿ã¾ã™ã€‚  

{{ figure(images=["images/ss-timedatectl.png"], caption="timedatectlã®å®Ÿè¡Œçµæœ") }}

ç„¡äº‹ã€time zoneãŒ`Asia/Tokyo`ã«å¤‰æ›´ã•ã‚Œã¦ã„ã‚‹ã“ã¨ãŒç¢ºèªã§ãã¾ã—ãŸã€‚

Part 3ã§ã¯raspiä¸Šã§serviceã‚’å‹•ã‹ã™ãŸã‚ã«å¿…è¦ãªsecretã‚’ç®¡ç†ã§ãã‚‹ã‚ˆã†ã«ã—ã¦ã„ãã¾ã™ã€‚


[Part 1]: https://blog.ymgyt.io/entry/homeserver-with-nixos-and-raspberrypi-install-nixos/
[deploy-rs]: https://github.com/serokell/deploy-rs